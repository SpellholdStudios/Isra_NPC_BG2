BACKUP ~isra_bg2/backup~
//AUTHOR ~Rhaella~
SUPPORT ~Rhaella and Spellhold staff at http://www.shsforums.net/forum/638-isra/~

VERSION ~v3.1~

README ~isra_bg2/readme/isra_bg2-readme-english.html~

ALWAYS

	ACTION_IF NOT VARIABLE_IS_SET bg2_chapter BEGIN // Always block must run only once per install!

		// Determines which game is installed and if the game is an Enhanced Edition.
		OUTER_SET is_ee = (GAME_IS ~bg2ee eet~) ? 1 : 0					// EE game
		OUTER_SET is_tob = (GAME_IS ~bgt tob tutu tutu_totsc~) ? 1 : 0	// based on vanilla ToB engine
		OUTER_SET is_bg2ee = (GAME_IS "bg2ee") ? 1 : 0					// b2gee
		OUTER_SET is_eet = (GAME_IS "eet") ? 1 : 0						// eet


		// Variable used by function GW_FIND_DLG_RESPONSE_STRING to find after which transition Isra will be recalled by Fate Spirit.
		OUTER_SET fatesp_string = 65017			// ~Bring me Imoen, the thief/mage.~


		/* eet compatibility */
		ACTION_IF is_eet BEGIN
			OUTER_SET bg2_chapter = 12
		END ELSE BEGIN
			OUTER_SET bg2_chapter = 0
		END
		OUTER_FOR (i = 1 ; i <= 10 ; ++i) BEGIN
			OUTER_SET bg2_chapter = bg2_chapter + 1
			OUTER_SPRINT name_source ~bg2_chapter_%i%~
			OUTER_SET EVAL ~%name_source%~ = bg2_chapter
		END


		// No exception, everything needs to be converted
		// setup.tra contains in game texts as well as installation texts and need to be converted
		ACTION_DEFINE_ARRAY rh#noconvert BEGIN END
		// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
		ACTION_DEFINE_ARRAY rh#reload BEGIN setup END

		LAF HANDLE_CHARSETS
			INT_VAR
				infer_charsets = 1
			STR_VAR
				tra_path = EVAL ~%MOD_FOLDER%/translations~
				noconvert_array = rh#noconvert
				reload_array = rh#reload
		END

	END

END


AUTO_TRA ~isra_bg2/translations/%s~

LANGUAGE ~English~
         ~english~
         ~isra_bg2/translations/english/setup.tra~


/* =============== *
 *  Isra for BGII  *
 * =============== */
BEGIN @0								 /* Isra for BGII */
DESIGNATED 0 LABEL ~isra_npc_for_bg2~
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1 /* You must have Throne of Bhaal or BG2:EE installed to install Isra NPC for BGII mod. */

INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~


/* ======================= *
 *  Isra Crossmod Content  *
 * ======================= */
BEGIN @2								/* Isra Crossmod Content */
DESIGNATED 1 LABEL ~isra_npc_for_bg2_crossmod_content~
REQUIRE_COMPONENT ~isra_bg2.tp2~ ~0~ @3	// ~Please install Isra NPC for BGII main component before additional content.~

INCLUDE ~%MOD_FOLDER%/lib/isra_bg2_crossmod.tpa~
